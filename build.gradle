plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.10'
    id 'io.spring.dependency-management' version '1.1.7'
    // id 'org.cyclonedx.bom' version '2.3.0' // 선택: SBOM 필요할 때만 켜
}

group = 'com.project'
version = '0.0.1-SNAPSHOT'
description = 'hanspoon'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    querydslVersion = "5.0.0"
}

dependencies {
    // REST API (React 분리)
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Validation (@Valid)
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // JPA (Entity로 테이블 자동 생성)
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Security (세션/쿠키 로그인 할 거면 유지)
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // MariaDB 드라이버
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    // JDBC 스타터는 기본적으로 불필요 (JPA만 쓰면 제거 추천)
    // - JdbcTemplate로 생쿼리 쓸 때만 켜
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // DevTools (개발용)
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // DTO <-> Entity 매핑
    implementation 'org.modelmapper:modelmapper:3.2.6'

    //타임리프
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // QueryDSL (검색/정렬/필터링 고급화)
    implementation "com.querydsl:querydsl-jpa:${querydslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // 소셜 API 연동
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'
    
    // .env 파일 로드
    implementation 'io.github.cdimascio:dotenv-java:3.0.0'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// QueryDSL QClass 생성 경로를 src/main/generated로 고정
def generated = file("src/main/generated")

tasks.withType(JavaCompile).configureEach {
    options.generatedSourceOutputDirectory = generated
}

sourceSets {
    main {
        java {
            srcDirs += generated
        }
    }
}

// gradle clean 시 QClass 삭제
clean {
    delete generated
}

tasks.named('test') {
    useJUnitPlatform()
}
