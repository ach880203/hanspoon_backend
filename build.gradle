plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.10'
    id 'io.spring.dependency-management' version '1.1.7'
    // id 'org.cyclonedx.bom' version '2.3.0' // 필요할 때만 사용
}

group = 'com.project'
version = '0.0.1-SNAPSHOT'
description = 'hanspoon'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    querydslVersion = "5.0.0"
}

dependencies {
    // Spring Web / Actuator
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // MariaDB
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    // JDBC (일부 기능에서 JdbcTemplate 사용)
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // DevTools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // DTO <-> Entity 매핑
    implementation 'org.modelmapper:modelmapper:3.2.6'

    // Thymeleaf
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // QueryDSL
    implementation "com.querydsl:querydsl-jpa:${querydslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // OAuth2 / WebFlux
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // .env 로더
    implementation 'io.github.cdimascio:dotenv-java:3.0.0'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// QueryDSL QClass 생성 경로
def generated = file("src/main/generated")

tasks.withType(JavaCompile).configureEach {
    options.generatedSourceOutputDirectory = generated
}

sourceSets {
    main {
        java {
            srcDirs += generated
        }
    }
}

// clean 시 생성된 QClass 삭제
clean {
    delete generated
}

tasks.named('test') {
    useJUnitPlatform()
}
